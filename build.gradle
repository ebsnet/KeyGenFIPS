/**
 * Gradle build file for Java EE projects
 * author: Valentin Brandl
 * created: 2017-06-20
 */

buildscript {
  repositories {
    maven {
      url 'http://192.168.118.4:8081/repository/maven-public/'
      allowInsecureProtocol true
    }
  }
}

// gradle plugins
plugins {
  id 'application'
  // auto format source files
  id 'com.diffplug.spotless' version '6.11.0'
  // analyze code and find potential bugs
  id 'pmd'
  id 'com.github.spotbugs' version '5.0.13'
  // check for vulnerable dependencies
  // EXPERIMENTAL(task has to be integrated with build process)
  id 'org.owasp.dependencycheck' version '7.0.4.1'
  // test coverage report
  id 'jacoco'
  // maven plugin to upload to repository
  id 'maven-publish'
  // notifications if there are dependency updates available (provides gradle task 'dependencyUpdates')
  id 'com.github.ben-manes.versions' version '0.51.0'
  // some lints for dependencies
  id 'nebula.lint' version '17.7.0'
  // resolve git branch name
  id 'org.ajoberstar.grgit' version '5.0.0'
}

group = 'de.ebsnet'
// version must be set _before_ applying helper.gradle
version = '1.0.0'

// include helper functions
apply from: resources.text.fromInsecureUri('http://192.168.118.4/EBSnet/documentation/gradle-doc/raw/master/helper.gradle')

if (!(isProdEnv || environment == 'check')) {
  // disable spotbugs, pmd on 'dev' builds
  [tasks.withType(Pmd), tasks.withType(com.github.spotbugs.snom.SpotBugsTask)].each {
    it.all { enabled = false }
  }
}

dependencyLocking {
  if (isProdEnv) {
    lockAllConfigurations()
  }
}

spotless {
  java {
    googleJavaFormat('1.15.0')
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.compilerArgs = [
    '-Xlint',
    '-g'
  ]
  // make build tasks depend on the formatter task
  // automatically format the code when building
  dependsOn 'spotlessApply'
}

tasks.withType(Javadoc).configureEach {
  options.encoding = 'UTF-8'
}

// log the build environment for all tasks from the build group
tasks.findAll { (it.group == 'build') } . each {
  it.doFirst {
    logger.warn((isProdEnv ? 'Production' : 'Development') + ' build')
  }
}

// use Java 11
sourceCompatibility = 1.11
targetCompatibility = 1.11

// dependency lints
gradleLint {
  // run all lints and print warnings
  rules = ['all-dependency']
  // fail the build if unused dependencies are defined
  criticalRules = ['unused-dependency']
  // do not run linter when the build fails
  gradleLint.autoLintAfterFailure = false
  // do not run linter on task publish
  skipForTask('publish')
  // skip linter on dependencycheck
  skipForTask('dependencyCheckAnalyze')
  // skip linter on IntelliJ init task
  skipForTask('prepareKotlinBuildScriptModel')
  skipForTask('ijDownloadSources')
}

// remote repositories for 3rd party dependencies
repositories {
  if (!isProdEnv) {
    // local maven repository
    mavenLocal()
  }
  maven {
    url 'http://192.168.118.4:8081/repository/maven-public/'
    allowInsecureProtocol true
  }
}

spotbugsMain {
  reports {
    xml.required.set(false)
    html.required.set(true)
  }
}

spotbugsTest {
  reports {
    xml.required.set(false)
    html.required.set(true)
  }
}

spotbugs {
  excludeFilter = file('config/spotbugs/exclude.xml')
}

tasks.withType(Pmd) {
  reports {
    xml.required.set(false)
    html.required.set(true)
  }
}

pmd {
  ruleSetFiles = files('config/pmd/pmd-all-java.xml')
  // this is needed for exclude patterns to work
  // this resets the default rulesets that are set by the gradle plugin itself
  ruleSets = []
}

def versions = [
  bc: '1.77',
]

dependencies {
  // implementation "org.bouncycastle:bcpkix-jdk18on:${versions.bc}"
  // implementation "org.bouncycastle:bcutil-jdk18on:${versions.bc}"
  // implementation "org.bouncycastle:bcprov-jdk18on:${versions.bc}"

 implementation 'org.bouncycastle:bc-fips:1.0.2.4'
 implementation 'org.bouncycastle:bcpkix-fips:1.0.7'

  implementation 'info.picocli:picocli:4.7.5'

//  testImplementation 'junit:junit:4.13.1'

  // additional spotbugs detectors
  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
  // TODO activate and fix warnings
  // spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.7'// EXPERIMENTAL
  // provides annotations to suppress spotbugs warnings
//  implementation "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
}

def latestCommitHash = grgit.head().abbreviatedId

// write application version to version property file
import org.apache.tools.ant.filters.*
processResources {
  filesNotMatching(["**/keys/*"]) {
    filter ReplaceTokens, tokens: [
      "version": project.property("version"),
      "commit": latestCommitHash
    ]
  }
}

// MANIFEST.mf properties
jar {
  manifest {
    attributes 'Name': project.name
    attributes 'Implementation-Version': latestCommitHash
    attributes 'Implementation-Vendor': 'EBSnet GmbH'
    attributes 'Specification-Version': archiveVersion
    attributes 'Specification-Vendor': 'EBSnet GmbH'
    attributes 'Main-Class': 'de.ebsnet.keygen.KeyGen'
  }

  // from {
  //   configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  // }
}

test {
  finalizedBy jacocoTestReport
}

dependencyCheck {
  failBuildOnCVSS = 7
  scanConfigurations = ['implementation', 'compileOnly', 'runtimeOnly', 'testImplementation', 'testRuntimeOnly', 'testCompileOnly']
}

application {
  // Define the main class for the application.
  mainClassName = 'de.ebsnet.keygen.KeyGen'
}

void prepare() {
  if (System.env['CI_JOB_ID'] == null) {
    String[] hooks = ['pre-commit', 'post-commit']
    hooks.findAll {
      !(new File(rootProject.rootDir, '.git/hooks/' + it).exists())
    }.each {
      println 'Linking ' + it
      java.nio.file.Files.createSymbolicLink(
        new File(rootProject.rootDir, '.git/hooks/' + it).toPath(),
        new File(rootProject.rootDir, 'hooks/' + it).toPath()
      )
    }
  }
}
prepare()
// vim: set filetype=groovy ts=2 sw=2 tw=120 et :
